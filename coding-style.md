# Verilog (SystemVerilog) Coding Style

## Выравнивание и отступы

Выравнивание необходимо для облегчения чтения кода.

- Отступ - 2 пробела. Табы `\t` запрещены.
```systemverilog
  statement_1
    statement_2 
```

- Круглые скобки оформляются так: `if( a )`: пробелы ставятся после открывающейся
  скобки и условия. Перед открывающеся скобкой пробел не ставится.

- Более сложный пример: 
```systemverilog
if( ( a == 5 ) && ( c > 8 ) )
```
Пробелами отделяется каждое из логических условий, причем, те условия которые находятся 
внутри `if` тоже отделяются пробелами.

### Пример выравнивания `if/else`

```systemverilog
if( a > 5 )
  d = 5;
else
  d = 15;
```

### Пример выравнивания `if/else` вместе с `begin/end`

```systemverilog
if( a > 5 )
  begin
    c = 7;
    d = 5;
  end
else
  begin
    c = 3;
    d = 7;
  end
```

### Пример выравнивания вложенного `if/else`

```systemverilog
if( a > 5 )
  c = 7;
else
  if( a > 3 )
    c = 4;
  else 
    c = 5;
```

- Следует стремиться выравнивать их названия, размерность и комментарии. 

Неправильно:

```systemverilog
// BAD EXAMPLE
logic rd_stb; //buffer read strobe
logic [31:0] ram_data; //data from RAM block
logic [1:0]if_mode; //interface mode
```

Правильно:

```systemverilog
// GOOD EXAMPLE
logic        rd_stb;    // buffer read strobe
logic [31:0] ram_data;  // data from RAM block
logic [1:0]  if_mode;   // interface mode
```

### Оператор `case`

Каждый из вариантов описывается в своем `begin/end` блоке, отделяя
варианты друг от друга пустой строкой.

```systemverilog
case( opcode[1:0] )
  2'b00:
    begin
      next_state = or_s;
    end

  2'b01:
    begin
      next_state = and_s;
    end

  2'b10:
    begin
      next_state = not_s;
    end

  2'b11:
    begin
      next_state = xor_s;
    end

  default:
    begin
      next_state = and_s;
    end
endcase
``` 

Если `case` простой и не подразумевает никаких вложенных конструкций в `begin/end` блоке, 
то допускается для уменьшения строк делать так:

```systemverilog
case( opcode[1:0] )
  2'b00:   next_state = or_s;
  2'b01:   next_state = and_s;
  2'b10:   next_state = not_s;
  2'b11:   next_state = xor_s;
  default: next_state = and_s;
endcase
```

Заметьте, что здесь выровнено по `next_state` для облегчения чтения.

### Еще один пример 
```systemverilog
if( condition1 )
  begin
    for( int i = 0; i < 10; i = i + 1 )
      statement1;
  end
else
  begin
    if( condition2 )
      statement2;
    else
      if( condition3 )
        statement3;
      else
        statement4;
  end
```

## Комментарии
Комментарии пишутся на английском языке.
После знака комментария `//` ставится один пробел.

## Наименование переменных и модулей

- Стиль названия переменных, функций, тасков и модулей - `snake_case`, т.е. слова или префиксы
  разделяются `_`, и всё пишется маленькими буквами.

  Пример:
    - `pkt_anlz`
    - `is_ptp_pkt`
    - `super_task`
  
- Большими буквами (но через `_`) пишутся только константы:
  - параметры: `parameter`, `localparam`
  - дефайны: `define`
  - состояния FSM

- Имя переменной должно отражать ее назначение. 
  Следует избегать чрезмерно длинных и, особенно, чрезмерно коротких названий (`rd_addr` лучше чем `ra`).

- Названия портов должны содержать суффикс `_i` для входных, `_o` для выходных, `_io` для 
  двунаправленных сигналов.

## Описание клоков
- Входной синхросигнал по умолчанию должен называться `clk_i`.

## Описание сбросов
Есть два типа сброса:
  - асинхронный (rst)
  - синхронный  (srst)

Чаще всего это используется как вход для описываемого модуля:
```systemverilog
...

  input     clk_i,
  input     rst_i,

...
```

Активным уровнем для асинхронного сброса считаем `1`, т.е. когда `rst_i == 1'b1`, 
то схема находится в ресете. 

Следовательно использование ресета для описания регистра будет таким:
```
always_ff @( posedge clk_i or posedge rst_i )
  if( rst_i )
    cnt <= 8'd0;
  else
    ...
```

Категорически запрещается:
  - путать синхронный и асинхронный сброс как по заведению сигналов, так и по их наименованию
  - создавать модули без сбросов (Исключение: модули без триггеров (чистая комбинационная логика)).

## Описание конечного автомата (FSM)

Для описания конечного автомата используется следующая схема (кое-где в интернете она ходит под названием `FSM 3 process`):

```systemverilog
enum { IDLE_S,
       RUN_S,
       WAIT_S } state, next_state;

always_ff @( posedge clk_i or posedge rst_i )
  if( rst_i )
    state <= IDLE_S;
  else
    state <= next_state;

always_comb
  begin
    next_state = state;

    case( state )
      IDLE_S:
        begin
          if( ... )
            next_state = RUN_S;
        end
      
      RUN_S:
        begin
          if( ... ) 
            next_state = WAIT_S;
          else
            if( )
              next_state = IDLE_S; 
        end

      WAIT_S:
        begin
          if( ... )
            next_state = RUN_S;
        end

      default:
        begin
          next_state = IDLE_S;
        end

    endcase
  end

// some logic, that generates output values on state and next_state signals


```

Пояснение:
  - `state` обозначает текущее состояние, `next_state` - то, которое будет в `state` на следующем такте.  
  - `IDLE_S` - дефолтное состояние, поэтому его устанавливают во время асинхронного сброса и `default` в `case`-блоке.
     для удобства считаем считаем, что это состояние должно идти первым в `enum` списке.

- Имена состояний FSM описываются большими буквами и должны содержать суффикс `_S` (`IDLE_S`, `TX_S`, `WAIT_S` и т.д.).

Категорически запрещается:
  - делать в одном модуле больше одного конечного автомата
  - в блоке, где происходит назначение на `next_state` делать назначение еще каких-либо сигналов

## Размещение исходников

Исходники размещаются в файлах. 
Правило простое: один модуль, package, интерфейса - один файл. Название файла - название этого модуля. 

- Расширения:
  - Verilog: `.v`
  - SystemVerilog: `.sv`
  - Verilog Header: `vh`
  - SystemVerilog Header: `svh`
  - VHDL: `.vhd`

- Файлы содержащие только декларации package следует помечать окончанием `_pkg`: `func_pkg.sv`
- Файлы содержащие только константами, функции, таски, которые подгружаются в исходник при помощи include, следует именовать расширением .vh Пример
`defines.vh`.

## Описание блоков
  - В одном блоке желательно описывать присваивание только в одну переменную.
  - Из разных блоков нельзя делать присваивания в одну переменную (не корректно, и не даст синтезироваться).
  
### Комбинационная логика
  - Описание комбинационных схем производится в блоках `always_comb`.
  - Разрешается использовать только блокирующие назначения (`a = b`).

#### Пример
```systemverilog
always_comb
  begin
    a = b + d;
  end
```

### Регистры
  - Разрешается использовать только неблокирующие присваивание (`a <= b`).
  - Блок должен быть отделён минимум одной пустой строкой свеху и снизу.

#### Пример 

``` systemverilog
logic [31:0] data_d1;
logic [7:0]  cnt;

// no async reset example
always_ff @( posedge clk_i )
  begin
    data_d1 <= data_i;
  end

// async reset example
always_ff @( posedge clk_i or posedge rst_i )
  if( rst_i )
    cnt <= '0;
  else
    if( cnt == 8'h92 )
      cnt <= 'd0;
    else
      cnt <= cnt + 1'd1;
```

Обращаю внимание на пробелы перед и после скобок.

## Описание и объявление модулей

### Описание модуля
Каждый модуль описывается в отдельном файле.

#### Пример

Файл `some_module.sv`:

```systemverilog
module some_module #(
  parameter DATA_WIDTH = 32,
  parameter CNT_WIDTH  = 8
)(
  input                         clk_i,
  input                         rst_i,

  input        [DATA_WIDTH-1:0] data_i,
  input                         data_val_i,

  output logic [CNT_WIDTH-1:0]  cnt_o
);

// some code...

endmodule
```

Т.е.:
  - Описание параметров и сигналов начинается с отступа (2 пробела).

  - При описании параметров и сигналов всё выравнивается по колонкам:
    - название сигналов 
    - квадратные скобки 
    - зарезервированные слова типа `parameter, `input, `output`
    - знак `=` в параметрах

  - Сигналы не смешиваются в кучу, те сигналы, которые формируют логический интерфейс
    интерфейсы должны отделятся пустой строкой.

  - Предпочтительно сначала описывать входные сигналы, потом выходные, однако,
    первочердным должен соблюдаться принцип интерфейсов, который описан выше.

  - Первым в описании сигналов должно идти описание клоков/синхросигналов и сбросов.  

### Инстанс модуля

```systemverilog
some_module #(
  .DATA_WIDTH                             ( 64                ),
  .CNT_WIDTH                              ( 4                 )
) some_module (
  .clk_i                                  ( rx_clk            ),
  .rst_i                                  ( main_rst          ),

  .data_i                                 ( rx_data           ),
  .data_val_i                             ( rx_data_val       ),

  .cnt_o                                  ( cnt               )
);  
```

Т.е.:
  - При подключении сигнала или переопределении параметра делается отступ (2 пробела).

  - Cкобки, точки, запятые выравниваются.

  - Для передачи параметров используется `#`, а не `defparam`.

  - Пустые строчки, которые отделяют логические интерфейсы (или сигналы, которые должны быть вместе) 
    расположены так же как и в описании модуля.

  - Чаще всего зкземпляр модуля называется так же как и сам модуль; 
    если экземпляров одного модуля несколько, то их названия 
    дополняются суффиксами `_inst0`, `_inst1` ( либо просто `0`, `1` ) и т.д. 
    Однако иногда названия получается слишком длинные и засчет вложенности модулей иерархический
    путь может быть очень длинным, что приведет к неудобству смотрения в ModelSim или TimeQuest.
    Поэтому допускается инстансы(!) модулей типа: `main_engine` сокращать до `me`. Однако без
    необходимости этим не злоупотреблять.

## Описание переменных

- Переменные в модулей "создаются" после описания сигналов модуля и до начала "работы" с этими сигналами.

```systemverilog
  output logic abc_o
);

logic [7:0]  cnt;
logic        cnt_en;

logic [63:0] pkt_data;
// ... some more signals

// some work with signal starts here
```

Выравнивание происходит тоже по колонкам:
  - тип переменной (`logic, `wire`, `reg`)
  - её размерность (`[7:0]`, `[31:0]`)
  - её имя 

- Связанные переменные, как и сигналы модуля необходимо логически отделять пустой строкой. 

- Допускается создавать сигналы "рядом" с тем местом, где они употребляются, например:
```systemverilog
logic [2:0] vlan_mpls_cnt;
// more signals

// some code
always_comb
  begin
    if( vlan_mpls_cnt > 2 )
      ...
  end

// calc vlan_mpls_cnt
logic [1:0] vlan_cnt;
logic [1:0] mpls_cnt;

assign vlan_cnt = vlan[0] + vlan[1] + vlan[2];
assign mpls_cnt = mpls[0] + mpls[1] + mpls[2];

assign vlan_mpls_cnt = vlan_cnt + mpls_cnt;
```

## Синтаксические конструкции

- При описании с помощью блока always комбинационной схемы, схемы содержащей
  регистры или защелки (latch) следует использовать соответственно блоки
  always_comb, always_ff или always_latch. Данное правило позволит избежать
  ошибок в описании (компилятор выдаст error) и сделает описание более компактным
  (к примеру, не будет надобности указывать список чувствительности для
  комбинационной логики).

- Категорически запрещается реализовывать в устройствах сигналы с нулевым
  активным уровнем. Исключением могут быть стандартные интерфейсы, где такие
  сигналы изначально предусмотрены (sram interface к примеру). Допускается
  наличие подобных сигналов на входах FPGA, но на верхнем уровне иерархии они
  должны быть проинвертированы.

- При наличии двунаправленных шин, направления передачи должны развязываться на
  верхнем уровне иерархии.

- В RTL описаниях использовать тип logic (за исключением тех случаев, когда
  необходимы множественные драйверы). Это позволит выявить ошибки, связанные с
  множественным назначением на этапе компиляции и ошибки, вызванные отсутствием
  инициализации (так как logic инициализируется неопределенным значением).

- Настоятельно рекомендуется записывать в регистры все валидные выходные сигналы. Это повысит максимальную скорость работы устройства.

- Все критичные к быстродействию узлы по возможности следует размещать в
  отдельном модуле. Это позволит оптимизировать его независимо от прочих узлов.

- Все макроподстановки (`define) должны определяться в отдельном файле либо в
  модуле верхнего уровня иерархии.

- Hard-code недопустим и, по возможности, модули должны быть параметризованы.
  Исключения возможны в тех случаях, когда параметризуемость делает программу
  менее читабельной и понятной. Главное требование - модуль не должен требовать
  допиливания после присвоения нового значения параметру, т.е. должен полностью
  сохранять функционал. 
